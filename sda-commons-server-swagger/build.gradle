dependencies {
  api project(':sda-commons-server-dropwizard')
  api project(':sda-commons-shared-yaml')

  api 'io.swagger:swagger-jaxrs', {
    /**
     * swagger-jaxrs uses the following dependency:
     *
     *         <!-- built with jsr311 but compatible with jsr311 or jsr399 in runtime -->
     *         <dependency>
     *             <groupId>javax.ws.rs</groupId>
     *             <artifactId>jsr311-api</artifactId>
     *             <version>1.1.1</version>
     *         </dependency>
     *
     * It conflicts with our 'javax.ws.rs:javax.ws.rs-api' which was added for Java 11.
     */
    exclude group: 'javax.ws.rs', module: 'jsr311-api'
    /**
     * Dropwizard comes with jakarta.validation instead of javax-validation.
     * Both contain the same classes.
     */
    exclude group: 'javax.validation', module: 'validation-api'
  }
  api 'jakarta.ws.rs:jakarta.ws.rs-api'
  api 'jakarta.validation:jakarta.validation-api'

  // We intentionally stick to the 1.0.4 version here since all updates only work with OpenAPI 3.0.
  // The sda-commons-server-openapi module uses a newer version that is compatible with the new
  // swagger-v3 library.
  api 'io.openapitools.hal:swagger-hal:2.0.2', {
    exclude group: 'io.swagger', module: 'swagger-jaxrs'
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  api 'io.openapitools.jackson.dataformat:jackson-dataformat-hal'

  api 'org.slf4j:slf4j-api'

  testImplementation project(':sda-commons-server-testing')
  testImplementation 'net.javacrumbs.json-unit:json-unit-assertj', {
    exclude group: 'org.hamcrest',  module: 'hamcrest-core'
  }
  testImplementation 'com.github.java-json-tools:json-schema-validator', {
    /**
     * Dropwizard comes with jakarta.activation instead of javax.activation.
     * Both contain the same classes.
     */
    exclude group: 'javax.activation', module: 'activation'
  }
  testImplementation 'jakarta.activation:jakarta.activation-api'
}

test {
  // We need to disable this property, otherwise Jersey doesn't allow to set the Origin header
  systemProperty "sun.net.http.allowRestrictedHeaders", "true"
}
