buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
    mavenCentral()
  }
}

plugins {
  id 'maven-publish'
  id 'jacoco'
  id 'signing'
  id 'org.sonarqube' version '4.4.1.3373'
  id 'com.diffplug.spotless' version '6.22.0'
  id 'io.codearte.nexus-staging' version '0.30.0'
  id 'project-report'
}

allprojects {
  apply plugin: 'idea'
  idea.module.inheritOutputDirs = true

  // Task for creating gradle.lockfile per module. Needed for Trivy vulnerability scan.
  task resolveAndLockAll {
    doFirst {
      assert gradle.startParameter.writeDependencyLocks
    }
    doLast {
      configurations.findAll {
        it.canBeResolved
      }.each { it.resolve() }
    }
  }
}

// we need a repository for the combination of jacoco and sonarcloud
repositories {
  mavenCentral()
}

/**
 * Some modules must use the 'java-platform'. Since you can't apply plugins 'java' and 'java-platform'
 * at the same time, they are excluded in some steps.
 */
List<String> javaPlatformModules = Arrays.asList("sda-commons-bom", "sda-commons-dependencies")

/**
 * There are some additional modules that are excluded from publishing
 */
List<String> unpublishedModules = javaPlatformModules + Arrays.asList("sda-commons-dependency-check")
project.ext.unpublishedModules = unpublishedModules

spotless {
  groovyGradle {
    greclipse()
    indentWithSpaces(2)
  }
}

/**
 * Settings for all modules
 */
subprojects {
  apply plugin: 'maven-publish'
  apply plugin: 'com.diffplug.spotless'
  apply plugin: 'signing'
  apply plugin: 'project-report'

  group 'org.sdase.commons'

  repositories {
    mavenCentral()
  }

  // create file that includes all dependencies in "build/deps"
  task allDeps(type: DependencyReportTask) {
    outputFile(new File("${rootProject.buildDir}/deps", "${project.name}.txt"))
  }
  check.dependsOn allDeps

  // Disable publication of Gradle Module Metadata
  tasks.withType(GenerateModuleMetadata) {
    enabled = false
  }

  spotless {
    if (!javaPlatformModules.contains(project.name)) {
      java {
        googleJavaFormat('1.17.0')
      }
    }
    groovyGradle {
      greclipse()
      indentWithSpaces(2)
    }
  }

  version = System.getenv('SEMANTIC_VERSION')

  // Don't configure publishing for the example projects
  if (!it.name.endsWith("-example")) {
    signing {
      def signingKey = findProperty("signingKey")
      def signingPassword = findProperty("signingPassword")
      useInMemoryPgpKeys(signingKey, signingPassword)
      sign publishing.publications
    }

    tasks.withType(Sign) {
      // skip signing a publication to maven local
      onlyIf { !gradle.taskGraph.hasTask(publishMavenPublicationToMavenLocal) }
    }

    publishing {
      publications {
        maven(MavenPublication) {
          pom {
            name = project.group + ":" + project.name
            description = 'A libraries to bootstrap services easily that follow the patterns and specifications promoted by the SDA SE'
            url = 'https://github.com/SDA-SE/sda-dropwizard-commons'

            licenses {
              license {
                name = 'MIT License'
                url = 'https://raw.githubusercontent.com/SDA-SE/sda-dropwizard-commons/master/LICENSE'
              }
            }

            organization {
              name = 'SDA SE Open Industry Solutions'
              url = 'https://sda.se'
            }

            issueManagement {
              system = 'GitHub'
              url = 'https://github.com/SDA-SE/sda-dropwizard-commons/issues'
            }

            developers {
              developer {
                id = 'maintainer'
                name = 'SDA SE Open Industry Solutions Maintainer'
                email = 'sda-dropwizard-commons@sda.se'
              }
            }

            scm {
              connection = 'scm:git:https://github.com/SDA-SE/sda-dropwizard-commons.git'
              developerConnection = 'scm:git:https://github.com/SDA-SE/sda-dropwizard-commons.git'
              url = 'https://github.com/SDA-SE/sda-dropwizard-commons'
            }
          }
        }
      }

      repositories {
        maven {
          name = "sdaIntern"
          def releasesRepoUrl = "https://nexus.sda-se.io/repository/sda-se-releases/"
          def snapshotsRepoUrl = "https://nexus.sda-se.io/repository/sda-se-snapshots/"
          url = version.endsWith('-SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

          credentials {
            username System.getenv('SDA_NEXUS_USER')
            password System.getenv('SDA_NEXUS_PASSWORD')
          }
        }
        maven {
          name 'mavenCentral'
          def releasesRepoUrl =
              System.getenv('SONATYPE_STAGING_REPOSITORY_ID')
              ? "https://oss.sonatype.org/service/local/staging/deployByRepositoryId/${System.getenv('SONATYPE_STAGING_REPOSITORY_ID')}"
              : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
          def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
          url = version.endsWith('-SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

          credentials {
            username System.getenv('SDA_SONATYPE_USER')
            password System.getenv('SDA_SONATYPE_PASSWORD')
          }
        }
      }
    }
  }
}

/**
 * Configure all regular submodules (all except the bom and dependency module)
 *
 * Problem is: You can't apply plugins 'java' and 'java-platform' at the same time.
 */
configure(subprojects.findAll { !javaPlatformModules.contains(it.name) }) {
  apply plugin: 'java-library'
  apply plugin: 'jacoco'

  // activate dependency locking for most configurations
  var ignoredConfigurationPatternsForDependencyLocking = Set.of("test.*", "jacoco.*")
  configurations.matching { configuration ->
    var match = !ignoredConfigurationPatternsForDependencyLocking.any { configuration.name.matches(it) }
    if (!match) {
      println "Ignoring dependency locking for '${it.name}:${configuration.name}'"
    }
    return match
  }.each {configuration ->
    println "Activating dependency locking for '${it.name}:${configuration.name}'"
    configuration.resolutionStrategy.activateDependencyLocking()
  }

  java {
    // preferring `sourceCompatibility` over `toolchains` to be able to do builds with different
    // Java versions
    sourceCompatibility = JavaVersion.VERSION_17
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  // Configure JaCoCo to export the reports in XML format
  jacocoTestReport {
    reports {
      xml.required = true
    }
  }

  // Configure sonar to use the aggregated global report file
  sonarqube {
    properties {
      property "sonar.coverage.jacoco.xmlReportPaths", '../build/reports/jacoco/report.xml'
    }
  }

  dependencies {
    api enforcedPlatform(project(':sda-commons-dependencies'))
  }

  configurations {
    all*.exclude group: 'javax.servlet', module: 'javax.servlet-api' // prefer jakarta.servlet-api
    all*.exclude group: 'org.glassfish.hk2.external', module: 'jakarta.inject' // prefer jakarta.inject:jakarta.inject-api
    all*.exclude group: 'commons-logging', module: 'commons-logging' // prefer jcl-over-slf4j
    all*. exclude group: 'org.eclipse.jetty.toolchain', module: 'jetty-jakarta-servlet-api' // prefer jakarta.servlet:jakarta-servlet-api
    all*. exclude group: 'com.sun.activation', module: 'jakarta.activation' // prefer jakarta.servlet:jakarta-activation
    all*. exclude group : 'org.apache.httpcomponents', module: 'httpclient' // prefer org.apache.httpcomponents.client5
  }

  test {
    useJUnitPlatform()
  }

  configurations.all {
    resolutionStrategy {
      // fail eagerly on version conflict (includes transitive dependencies)
      // e.g. multiple different versions of the same dependency (group and name are equal)
      failOnVersionConflict()
    }
  }
}

/**
 * Add publishing configuration for Java modules.
 *
 * More Infos:
 * - sda-commons-bom and sda-commons-dependencies uses own publishing because it's just a BOM
 * - example modules and dependency-check should not be published at all
 */
configure(subprojects.findAll {
  !unpublishedModules.contains(it.name) && !it.name.endsWith("-example")
}) {
  java {
    withJavadocJar()
    withSourcesJar()
  }

  javadoc {
    options.addStringOption('encoding', 'UTF-8')
  }

  publishing {
    publications {
      maven(MavenPublication) {
        from components.java
      }
    }
  }

  jar {
    into("META-INF/maven/$project.group/$project.name") {
      from { generatePomFileForMavenPublication }
      rename ".*", "pom.xml"
    }
  }
}

/**
 * Add publishing configuration for Java platforms.
 */
configure(subprojects.findAll { javaPlatformModules.contains(it.name) }) {
  apply plugin: 'java-platform'

  publishing {
    publications {
      maven(MavenPublication) {
        from components.javaPlatform
      }
    }
  }
}

// Reconfigure the testReport task to display the results of all modules into a single report
task testReport(type: TestReport) {
  destinationDirectory = project.layout.buildDirectory.dir("reports/allTests")
  // Include the results from the `test` task in all subprojects
  testResults.from(
      subprojects.findAll { !javaPlatformModules.contains(it.name) }*.test.binaryResultsDirectory
      )
}

// Create a combined XML report of all modules in the root project
task codeCoverageReport(type: JacocoReport) {
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

  subprojects.findAll { !unpublishedModules.contains(it.name) }.each {
    // noinspection GroovyAssignabilityCheck
    sourceSets it.sourceSets.main
  }

  reports {
    xml.required = true
    xml.outputLocation = project.layout.buildDirectory.file("reports/jacoco/report.xml")
  }

  dependsOn ":testReport"
}

// Automatically close and release the staging repository that gets created
// during the upload to maven central.
nexusStaging {
  username System.getenv('SDA_SONATYPE_USER')
  password System.getenv('SDA_SONATYPE_PASSWORD')
  packageGroup "org.sdase.commons"
  // Read the stagingRepositoryId from the environment
  stagingRepositoryId.set(System.getenv('SONATYPE_STAGING_REPOSITORY_ID'))
}

sonarqube {
  properties {
    property 'sonar.projectKey', 'SDA-SE_sda-dropwizard-commons'
    property 'sonar.organization', 'sda-se'
    property 'sonar.host.url', 'https://sonarcloud.io'
    property 'sonar.token', System.getenv('SONAR_LOGIN_TOKEN')

    property 'sonar.pullrequest.provider', 'GitHub'
    property 'sonar.pullrequest.github.repository', System.getenv('GITHUB_REPOSITORY')

    if (System.getenv('GITHUB_PR') != null) {
      property 'sonar.pullrequest.key', System.getenv('GITHUB_PR')
    }

    if (System.getenv('SONAR_SCM_REVISION') != null) {
      property 'sonar.scm.revision', System.getenv('SONAR_SCM_REVISION')
    }
  }
}
