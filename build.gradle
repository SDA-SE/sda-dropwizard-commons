buildscript {
  repositories {
    maven { url = "https://plugins.gradle.org/m2/" }
    mavenCentral()
  }
}

plugins {
  id 'jacoco'
  id 'idea'
  id 'com.vanniktech.maven.publish' version '0.36.0'
  id 'com.diffplug.spotless' version '8.1.0'
  id 'org.sonarqube' version '7.1.0.6387' //check that coverage works before updating; v7.2.2.6593 didn't worked
  id 'project-report'
  id 'io.codearte.nexus-staging' version '0.30.0'
  id 'org.cyclonedx.bom' version '3.1.0'
}

import org.cyclonedx.Version

tasks.cyclonedxBom {
  schemaVersion = Version.VERSION_14
}

repositories {
  mavenCentral()
}

group = 'org.sdase.commons'
// the version of the generated jars is based on the SEMANTIC_VERSION environment variable
version = System.getenv('SEMANTIC_VERSION')

allprojects {
  apply plugin: 'idea'
  idea.module.inheritOutputDirs = true

  // Task for creating gradle.lockfile per module. Needed for Trivy vulnerability scan.
  tasks.register('resolveAndLockAll') {
    doFirst {
      assert gradle.startParameter.writeDependencyLocks
    }
    doLast {
      configurations.findAll {
        it.canBeResolved
      }.each { it.resolve() }
    }
  }
}

/**
 * Some modules must use the 'java-platform'. Since you can't apply plugins 'java' and 'java-platform'
 * at the same time, they are excluded in some steps.
 */
List<String> javaPlatformModules = Arrays.asList('sda-commons-dependencies', 'sda-commons-bom')

/**
 * There are some additional modules that are excluded from publishing
 */
List<String> unpublishedModules = javaPlatformModules + Arrays.asList('sda-commons-dependency-check')
project.ext.unpublishedModules = unpublishedModules

spotless {
  groovyGradle {
    greclipse()
    leadingTabsToSpaces(2)
  }
}

subprojects {
  apply plugin: 'signing'
  apply plugin: 'com.diffplug.spotless'
  apply plugin: 'project-report'

  repositories {
    mavenCentral()
  }

  group = rootProject.group
  version = rootProject.version

  // Disable publication of Gradle Module Metadata
  tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
  }

  spotless {
    if (!javaPlatformModules.contains(project.name)) {
      java {
        googleJavaFormat('1.17.0')
      }
      groovyGradle {
        greclipse()
        leadingTabsToSpaces(2)
      }
    }
  }

  // Don't configure publishing for the example projects
  if (!it.name.endsWith("-example")) {
    apply plugin: "com.vanniktech.maven.publish"

    tasks.withType(Sign).configureEach {
      // skip signing a publication to maven local
      onlyIf { !gradle.taskGraph.hasTask(publishToMavenLocal) }
    }

    mavenPublishing {
      publishToMavenCentral(true)
      signAllPublications()

      pom {
        name = project.group + ":" + project.name
        description = 'A libraries to bootstrap services easily that follow the patterns and specifications promoted by the SDA SE'
        url = 'https://github.com/SDA-SE/sda-dropwizard-commons'

        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/SDA-SE/sda-dropwizard-commons/main/LICENSE'
          }
        }

        organization {
          name = 'SDA SE Open Industry Solutions'
          url = 'https://sda.se'
        }

        issueManagement {
          system = 'GitHub'
          url = 'https://github.com/SDA-SE/sda-dropwizard-commons/issues'
        }

        developers {
          developer {
            id = 'maintainer'
            name = 'SDA SE Open Industry Solutions Maintainer'
            email = 'sda-dropwizard-commons@sda.se'
          }
        }

        scm {
          url = 'https://github.com/SDA-SE/sda-dropwizard-commons'
          connection = 'scm:git:https://github.com/SDA-SE/sda-dropwizard-commons.git'
          developerConnection = 'scm:git:https://github.com/SDA-SE/sda-dropwizard-commons.git'
        }
      }
    }
  }
}

/**
 * Configure all regular submodules (all except the bom and dependency module)
 *
 * Problem is: You can't apply plugins 'java' and 'java-platform' at the same time.
 */
configure(subprojects.findAll { !javaPlatformModules.contains(it.name) }) {
  apply plugin: 'jacoco'
  apply plugin: 'java-library'

  // activate dependency locking for most configurations
  var ignoredConfigurationPatternsForDependencyLocking = Set.of("test.*")
  configurations.matching { configuration ->
    !ignoredConfigurationPatternsForDependencyLocking.any { configuration.name.matches(it) }
  }.each {configuration ->
    // println "Activating dependency locking for '${it.name}:${configuration.name}'"
    configuration.resolutionStrategy.activateDependencyLocking()
  }

  java {
    // preferring `sourceCompatibility` over `toolchains` to be able to do builds with different
    // Java versions
    sourceCompatibility = JavaVersion.VERSION_17
  }

  tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
  }

  jacoco {
    toolVersion = "0.8.12"
  }
  // Configure JaCoCo to export the reports in XML format
  jacocoTestReport {
    reports {
      xml.required = true
    }
  }

  // Configure sonar to use the aggregated global report file
  sonarqube {
    properties {
      property "sonar.coverage.jacoco.xmlReportPaths", '../build/reports/jacoco/report.xml'
    }
  }

  configurations {
    all*.exclude group: 'javax.servlet', module: 'javax.servlet-api' // prefer jakarta.servlet-api
    all*.exclude group: 'org.glassfish.hk2.external', module: 'jakarta.inject' // prefer jakarta.inject:jakarta.inject-api
    all*.exclude group: 'commons-logging', module: 'commons-logging' // prefer jcl-over-slf4j
    all*. exclude group: 'org.eclipse.jetty.toolchain', module: 'jetty-jakarta-servlet-api' // prefer jakarta.servlet:jakarta-servlet-api
    all*. exclude group: 'com.sun.activation', module: 'jakarta.activation' // prefer jakarta.servlet:jakarta-activation
    all*. exclude group : 'org.apache.httpcomponents', module: 'httpclient' // prefer org.apache.httpcomponents.client5
  }

  test {
    useJUnitPlatform()
  }

  configurations.configureEach {
    if (it.name.startsWith("spotless")) {
      // Workaround for https://github.com/diffplug/spotless/issues/1965.
    } else {
      it.resolutionStrategy {
        failOnVersionConflict()
      }
    }
  }

  dependencies {
    api enforcedPlatform(project(':sda-commons-dependencies'))
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  }
}

/**
 * Add publishing configuration for Java modules.
 *
 * More Infos:
 * - sda-commons-bom and sda-commons-dependencies uses own publishing because it's just a BOM
 * - example modules and dependency-check should not be published at all
 */
configure(subprojects.findAll {
  !unpublishedModules.contains(it.name) && !it.name.endsWith("-example")
}) {

  javadoc {
    options.addStringOption('encoding', 'UTF-8')
  }

  jar {
    into("META-INF/maven/$project.group/$project.name") {
      from { generatePomFileForMavenPublication }
      rename ".*", "pom.xml"
    }
  }
}

/**
 * Add publishing configuration for Java platforms.
 */
configure(subprojects.findAll { javaPlatformModules.contains(it.name) }) {
  apply plugin: 'java-platform'
}

// Reconfigure the testReport task to display the results of all modules into a single report
tasks.register("testReport", TestReport) {
  destinationDirectory = project.layout.buildDirectory.dir("reports/allTests")
  // Include the results from the `test` task in all subprojects
  testResults.from(
      subprojects.findAll { !javaPlatformModules.contains(it.name) }*.test.binaryResultsDirectory
      )
}

// Create a combined XML report of all modules in the root project
tasks.register("codeCoverageReport", JacocoReport) {
  dependsOn = [
    testReport,
    spotlessGroovyGradle
  ]

  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

  subprojects.findAll { !javaPlatformModules.contains(it.name) }.each {
    // noinspection GroovyAssignabilityCheck
    sourceSets it.sourceSets.main
    codeCoverageReport.dependsOn("${it.path}:spotlessJava")
    codeCoverageReport.dependsOn("${it.path}:spotlessGroovyGradle")
  }

  reports {
    xml.required = true
    xml.outputLocation = project.layout.buildDirectory.file("reports/jacoco/report.xml")
  }
}

sonarqube {
  properties {
    property 'sonar.projectKey', 'SDA-SE_sda-dropwizard-commons'
    property 'sonar.organization', 'sda-se'
    property 'sonar.host.url', 'https://sonarcloud.io'
    property 'sonar.token', System.getenv('SONAR_LOGIN_TOKEN')

    property 'sonar.pullrequest.provider', 'GitHub'
    property 'sonar.pullrequest.github.repository', System.getenv('GITHUB_REPOSITORY')

    if (System.getenv('GITHUB_PR') != null) {
      property 'sonar.pullrequest.key', System.getenv('GITHUB_PR')
    }

    if (System.getenv('SONAR_SCM_REVISION') != null) {
      property 'sonar.scm.revision', System.getenv('SONAR_SCM_REVISION')
    }
  }
}
