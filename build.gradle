buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '4.0.2'
    id 'org.sonarqube' version '2.8'
}

allprojects {
    apply plugin: 'idea'
    idea.module.inheritOutputDirs = true
}

subprojects {
    apply plugin: 'maven-publish'

    group 'org.sdase.commons'

    repositories {
        mavenCentral()
        maven { url "http://packages.confluent.io/maven/" } // for sda-commons-server-kafka-confluent-testing only
    }

    // TODO: reenable this setting on a new ci environment
    // tasks.withType(Test) {
    //     maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    // }

    version = System.getenv('SEMANTIC_VERSION')

    publishing {
        repositories {
            maven {
                def releasesRepoUrl = "https://nexus.intern.sda-se.online/repository/sda-se-releases/"
                def snapshotsRepoUrl = "https://nexus.intern.sda-se.online/repository/sda-se-snapshots/"
                url = version.endsWith('-SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username System.getenv('SDA_NEXUS_USER')
                    password System.getenv('SDA_NEXUS_PASSWORD')
                }
            }
        }
    }
}

/**
 * Configure all regular submodules (all except the bom module)
 *
 * Problem is: You can't apply plugins 'java' and 'java-platform' at the same time.
 */
configure(subprojects.findAll {it.name != 'sda-commons-bom' && it.name != 'sda-commons-dependencies'}) {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.owasp.dependencycheck'

    sourceCompatibility = 1.8

    dependencies {
        compile enforcedPlatform(project(':sda-commons-dependencies'))
    }

    sourceSets {
        main {
            output.resourcesDir = output.classesDirs.singleFile
        }
        test {
            output.resourcesDir = output.classesDirs.singleFile
        }
    }

    configurations.all {
        resolutionStrategy {
            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()
        }
    }
}

/**
 * Add publishing configuration for Java modules.
 *
 * More Infos:
 * - sda-commons-bom uses own publishing because it's just a BOM
 * - example modules and dependency-check should not be published at all
 */
List<String> unpublishedModules = Arrays.asList("sda-commons-bom", "sda-commons-dependencies", "sda-commons-dependency-check")
configure(subprojects.findAll {
    !unpublishedModules.contains(it.name) && !it.name.endsWith("-example")
}) {

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourcesJar
            }
        }
    }

    jar {
        into("META-INF/maven/$project.group/$project.name") {
            from { generatePomFileForMavenPublication }
            rename ".*", "pom.xml"
        }
    }
}

// Reconfigure the testReport task to display the results of all modules into a single report
task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects.findAll {it.name != 'sda-commons-bom' && it.name != 'sda-commons-dependencies'}*.test
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'SDA-SE_sda-commons'
        property 'sonar.organization', 'sda-se'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.login', System.getenv('SONAR_LOGIN_TOKEN')

        property 'sonar.pullrequest.provider', 'GitHub'
        property 'sonar.pullrequest.github.repository', System.getenv('GITHUB_REPOSITORY')

        if (System.getenv('GITHUB_PR') != null) {
            property 'sonar.pullrequest.key', System.getenv('GITHUB_PR')
        }
    }
}
