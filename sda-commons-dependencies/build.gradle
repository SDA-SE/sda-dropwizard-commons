plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  dropwizardVersion = '4.0.4'
  prometheusVersion = '0.16.0'
  micrometerVersion = '1.12.0'
  swaggerCoreV3Version = '2.2.19'
  weldVersion = '5.1.2.Final'
  jacksonVersion = '2.16.0'
  jsonUnitVersion = '2.38.0'
  scalaVersion = '2.13.12' // align transitive dependency from various modules, keep up to date
  kafkaVersion = '3.6.0'
  kafkaJunitVersion = '3.2.5' // Matching version: https://github.com/salesforce/kafka-junit/tree/master/kafka-junit5
  dbRiderVersion = '1.41.0'
  kotlinVersion = '1.9.21'
  kotlinxCoroutinesVersion = '1.7.3'
  resilience4jVersion = '2.1.0'
  openTelemetryVersion= '1.32.0'
  // should be aligned with transitive dependency of Spring Data MongoDB
  mongoDbDriverVersion = '4.6.1'
  tuprologVersion = '0.20.9' // don't upgrade separately! use version from database-rider
  bouncyCastleVersion = '1.77'
}

dependencies {
  // overwrite version used by Dropwizard to comply with Snakeyaml 2.0
  api enforcedPlatform("com.fasterxml.jackson:jackson-bom:${jacksonVersion}")
  // override version from dropwizard-bom
  api enforcedPlatform("org.jetbrains.kotlin:kotlin-bom:$kotlinVersion"), {
    exclude group: 'org.jetbrains', module: 'annotations'
    because("this version is outdated, but the main dependency does not have yet an updated version")
  }
  api enforcedPlatform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:$kotlinxCoroutinesVersion"), {
    exclude group: 'org.jetbrains', module: 'annotations'
    because("this version is outdated, but the main dependency does not have yet an updated version")
  }
  api enforcedPlatform("io.dropwizard:dropwizard-bom:$dropwizardVersion")
  api enforcedPlatform("io.dropwizard:dropwizard-dependencies:$dropwizardVersion"), {
    exclude group: 'org.awaitility', module: 'awaitility'
    exclude group: 'org.assertj', module: 'assertj-core'
    exclude group: 'jakarta.xml.bind', module: 'jakarta.xml.bind-api'
    exclude group: 'com.fasterxml.jackson'
    exclude group: 'com.fasterxml.jackson.core'
    exclude group: 'com.fasterxml.jackson.dataformat'
    exclude group: 'com.fasterxml.jackson.datatype'
    exclude group: 'com.fasterxml.jackson.jaxrs'
    exclude group: 'com.fasterxml.jackson.module'
    exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
  }
  api enforcedPlatform("com.amazonaws:aws-java-sdk-bom:1.12.602")
  api enforcedPlatform("io.opentelemetry:opentelemetry-bom:$openTelemetryVersion")
  api enforcedPlatform("io.opentelemetry:opentelemetry-bom-alpha:${openTelemetryVersion}-alpha")
  api enforcedPlatform("io.netty:netty-bom:4.1.101.Final"), {
    because "various CVEs in earlier versions"
  }

  constraints {
    // overall conflicts
    api "org.apache.commons:commons-lang3:3.14.0"
    api "commons-fileupload:commons-fileupload:1.5", {
      because "CVE-2023-24998 in 1.4"
    }
    api 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    api 'org.apache.httpcomponents.client5:httpclient5-fluent:5.2.1'
    api "io.github.classgraph:classgraph:4.8.165", {
      because "conflict between io.swagger.core.v3:swagger-jaxrs2, io.swagger.core.v3:swagger-integration, dev.morphia.morphia:core and com.kjetland:mbknor-jackson-jsonschema_2.13 upgrade"
    }
    api "org.jboss.logging:jboss-logging:3.5.3.Final", {
      because "conflict between org.hibernate.validator:hibernate-validator and org.jboss.weld.se:weld-se-core"
    }
    api "org.yaml:snakeyaml:2.2", {
      because "conflict between com.github.database-rider:rider-core and com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
      because "vulnerability CVE-2022-1471 in 1.33 and below"
    }

    // overall testing dependencies
    api "org.awaitility:awaitility:4.2.0"
    api 'org.assertj:assertj-core:3.24.2'

    api 'jakarta.annotation:jakarta.annotation-api:2.1.1'

    // sda-commons-client-jersey-wiremock-testing
    api "org.wiremock:wiremock:3.3.1"
    api "org.ow2.asm:asm:9.6", {
      because "version conflict within Wiremock"
    }
    api "net.minidev:json-smart:2.5.0", {
      because 'conflict between json-path 2.7.0 (2.4.7) and wirmock-jre8 (2.4.7 via json-path and 2.4.8 directly)'
    }

    // sda-commons-shared-asyncapi
    api "com.kjetland:mbknor-jackson-jsonschema_2.13:1.0.39"

    // sda-commons-server-auth
    api "com.auth0:java-jwt:4.4.0"
    api "org.bouncycastle:bcpkix-jdk18on:$bouncyCastleVersion"
    api "org.bouncycastle:bcprov-jdk18on:$bouncyCastleVersion", {
      because 'CVE-2023-33201 in 1.70'
    }

    // sda-commons-server-circuitbreaker
    api "io.github.resilience4j:resilience4j-circuitbreaker:$resilience4jVersion"
    api "io.github.resilience4j:resilience4j-micrometer:${resilience4jVersion}"
    api "org.objenesis:objenesis:3.3"

    // sda-commons-server-hibernate
    api "org.postgresql:postgresql:42.7.0"
    api "org.flywaydb:flyway-core:9.22.3"

    // sda-commons-server-hibernate-testing
    api "com.github.database-rider:rider-core:$dbRiderVersion"
    api "com.github.database-rider:rider-junit5:$dbRiderVersion"
    api "it.unibo.tuprolog:solve-classic-jvm:$tuprologVersion"
    api "it.unibo.tuprolog:parser-theory-jvm:$tuprologVersion"

    // sda-commons-server-mongo-testing, sda-commons-server-spring-data-mongo
    api "de.flapdoodle.embed:de.flapdoodle.embed.mongo:4.11.1"
    api "io.opentelemetry.instrumentation:opentelemetry-mongo-3.1:${openTelemetryVersion}-alpha"
    api "org.mongodb:mongodb-driver-core:${mongoDbDriverVersion}"
    api "org.mongodb:mongodb-driver-sync:${mongoDbDriverVersion}"
    api "org.mongodb:mongodb-driver-legacy:${mongoDbDriverVersion}"

    // sda-commons-server-openapi
    api "com.jayway.jsonpath:json-path:2.8.0"

    // sda-commons-server-openapi
    api "io.openapitools.jackson.dataformat:jackson-dataformat-hal:1.0.9" // TODO Needed for OpenAPI?
    api "io.swagger.core.v3:swagger-jaxrs2-jakarta:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-jaxrs2-servlet-initializer-v2-jakarta:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-annotations-jakarta:$swaggerCoreV3Version"
    api "io.swagger.parser.v3:swagger-parser-v3:2.1.19"

    // sda-commons-server-prometheus
    api 'io.prometheus:prometheus-metrics-simpleclient-bridge:1.1.0'
    api "io.prometheus:simpleclient:$prometheusVersion"
    api "io.prometheus:simpleclient_common:$prometheusVersion"
    api "io.prometheus:simpleclient_dropwizard:$prometheusVersion"
    api "io.prometheus:simpleclient_servlet:$prometheusVersion"

    api "io.micrometer:micrometer-core:$micrometerVersion"
    api "io.micrometer:micrometer-registry-prometheus:$micrometerVersion"

    // sda-commons-server-s3-testing
    api "io.findify:s3mock_2.13:0.2.6"
    api "io.opentelemetry.instrumentation:opentelemetry-aws-sdk-1.11:${openTelemetryVersion}-alpha"

    // sda-commons-server-swagger
    api "com.github.java-json-tools:json-schema-validator:2.2.14"
    api "net.javacrumbs.json-unit:json-unit-assertj:$jsonUnitVersion"

    // sda-commons-server-testing
    api "org.hamcrest:hamcrest:2.2", {
      because "hamcrest is the successor of hamcrest-core."
    }
    api "org.hamcrest:hamcrest-core:2.2", {
      because '''\
          hamcrest-core is deprecated and overlaps with hamcrest, in hamcrest-core 2.2 is
          only one class named HamcrestCoreIsDeprecated. We force this version to avoid complex
          exclude and dependency substitution configuration as there is no conflict with
          hamcrest in the code base. Please do not upgrade hamcrest-core.
          '''
    }

    // sda-commons-server-weld
    api "org.jboss.weld.se:weld-se-core:$weldVersion"
    api "org.jboss.weld.servlet:weld-servlet-core:$weldVersion"
    api "jakarta.transaction:jakarta.transaction-api:2.0.1", {
      because "Dropwizard does not use the jakarta version yet so we replace it in weld with the javax version that comes as transitive dependency without explicit version."
    }

    api "org.junit-pioneer:junit-pioneer:2.2.0"

    // sda-commons-shared-*
    api 'commons-io:commons-io:2.15.1'

    // sda-commons-server-opentelemetry
    api "io.opentelemetry:opentelemetry-sdk-extension-autoconfigure:${openTelemetryVersion}"
    api "io.opentelemetry.javaagent.instrumentation:opentelemetry-javaagent-apache-httpclient-5.0:${openTelemetryVersion}-alpha"
    api "io.opentelemetry:opentelemetry-exporter-otlp:$openTelemetryVersion"
    api "io.opentelemetry:opentelemetry-exporter-jaeger:$openTelemetryVersion"
    api "io.opentelemetry:opentelemetry-extension-trace-propagators:$openTelemetryVersion"
    api "io.opentelemetry:opentelemetry-sdk-testing:$openTelemetryVersion"
    api "io.opentelemetry:opentelemetry-exporter-logging:$openTelemetryVersion"
    //api "io.opentelemetry:opentelemetry-semconv:${openTelemetryVersion}"
    api "io.opentelemetry.instrumentation:opentelemetry-instrumentation-api-semconv:${openTelemetryVersion}-alpha"
    api "com.squareup.okhttp3:okhttp:4.12.0" // bump from 4.9.3 used in opentelemetry-exporter-otlp-common
    api 'com.squareup.okio:okio:3.6.0', {
      because 'CVE-2023-3635 in 3.2.0'
    }

    // Kafka
    api "org.apache.kafka:kafka_2.13:$kafkaVersion"
    api "org.apache.kafka:kafka-clients:$kafkaVersion"
    api "org.apache.zookeeper:zookeeper:3.8.3" // Kafka Client 3.6.0 also uses 3.x
    api "com.salesforce.kafka.test:kafka-junit5:$kafkaJunitVersion"
    api "org.apache.curator:curator-test:5.5.0", {
      because '''\
          Upgrade because kafka-junit still uses curator 2.12.0.
          The older version does not work when executing tests parallel because it starts the
          Zookeeper Admin Server on a fixed port. More recent versions do not start the admin server
          anymore.
        '''
    }
    api "org.xerial.snappy:snappy-java:1.1.10.5", {
      because "conflict between org.apache.kafka:kafka-clients and org.apache.curator:curator-test upgrade"
    }
    api "org.scala-lang.modules:scala-java8-compat_2.13:1.0.2", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13 upgrade"
    }
    api "org.scala-lang.modules:scala-collection-compat_2.13:2.11.0", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13 upgrade"
    }
    api "org.scala-lang:scala-library:$scalaVersion", {
      because "conflict between org.apache.kafka:kafka_2.13, io.findify:s3mock_2.13 and com.kjetland:mbknor-jackson-jsonschema_2.13:+ upgrade"
    }
    api "org.scala-lang:scala-reflect:$scalaVersion", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13"
    }
    api "com.typesafe.scala-logging:scala-logging_2.13:3.9.5", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13"
    }

    api "jakarta.xml.bind:jakarta.xml.bind-api:4.0.1"
  }
}
