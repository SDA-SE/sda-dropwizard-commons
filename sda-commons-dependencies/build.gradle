plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  dropwizardVersion = '2.0.25'
  morphiaVersion = '1.6.1'
  prometheusVersion = '0.9.0'
  swaggerCoreVersion = '1.6.3'
  swaggerCoreV3Version = '2.1.11'
  weldVersion = '3.1.8.Final'
  openTracingUtilVersion = '0.33.0'
  jsonUnitVersion = '2.28.0'
  scalaVersion = '2.12.12' // Always prefer the version from kafka_2.12. Make sure that the major version is always aligned with kafka, s3mock, and mbknor-jackson-jsonschema.
  kafkaVersion = '2.8.1'
  kafkaJunitVersion = '3.2.3' // Matching version: https://github.com/salesforce/kafka-junit/tree/master/kafka-junit5
  dbRiderVersion = '1.31.0'
}

dependencies {
  api enforcedPlatform("org.jetbrains.kotlin:kotlin-bom:1.3.50") // override version from dropwizard-bom
  api enforcedPlatform("io.dropwizard:dropwizard-bom:$dropwizardVersion")
  api enforcedPlatform("io.dropwizard:dropwizard-dependencies:$dropwizardVersion")
  api enforcedPlatform("com.amazonaws:aws-java-sdk-bom:1.12.83")
  api enforcedPlatform("io.github.resilience4j:resilience4j-bom:1.7.0")

  constraints {
    // overall conflicts
    api "commons-codec:commons-codec:1.15", {
      because "conflict between org.apache.httpcomponents:httpclient:4.5.12 and org.dbunit:dbunit:2.7.0"
    }
    api "io.github.classgraph:classgraph:4.8.109", {
      because "conflict between com.kjetland:mbknor-jackson-jsonschema_2.12 and dev.morphia.morphia:core"
    }
    api "net.sf.jopt-simple:jopt-simple:5.0.4", {
      because "conflict between org.apache.kafka:kafka_2.12 and com.github.tomakehurst:wiremock-jre8"
    }
    api "org.jboss.logging:jboss-logging:3.4.2.Final", {
      because "conflict between org.hibernate.validator:hibernate-validator and org.jboss.weld.se:weld-se-core"
    }
    api "org.yaml:snakeyaml:1.29", {
      because "conflict between com.github.ftrossbach:club-topicana-core and com.github.database-rider:rider-core and com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    }
    api "net.javacrumbs.json-unit:json-unit-core:$jsonUnitVersion", {
      because "conflict between com.github.tomakehurst:wiremock-jre8 and net.javacrumbs.json-unit:json-unit-assertj"
    }
    // TODO: Cleanup after dropwizard itself updated to >= 1.1.3.
    api "io.dropwizard.logback:logback-throttling-appender:1.1.3", {
      because "version from dropwizard has a conflict with com.google.j2objc:j2objc-annotations"
    }

    // overall testing dependencies
    api "org.awaitility:awaitility:4.1.0"

    // sda-commons-client-jersey-wiremock-testing
    api "com.github.tomakehurst:wiremock-jre8:2.28.1"

    // sda-commons-server-asyncapi
    api "com.kjetland:mbknor-jackson-jsonschema_2.12:1.0.39"

    // sda-commons-server-auth
    api "com.auth0:java-jwt:3.18.2"

    // sda-commons-server-circuitbreaker
    api "org.objenesis:objenesis:3.2"

    // sda-commons-server-hibernate
    api "org.postgresql:postgresql:42.2.24"
    api "org.flywaydb:flyway-core:6.5.5"

    // sda-commons-server-hibernate-testing
    api "com.github.database-rider:rider-core:$dbRiderVersion"
    api "com.github.database-rider:rider-junit5:$dbRiderVersion"

    // sda-commons-server-jaeger
    api "io.jaegertracing:jaeger-client:1.6.0"

    // sda-commons-server-mongo-testing
    api "de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.0.0"

    // sda-commons-server-morphia
    api "dev.morphia.morphia:core:$morphiaVersion"
    api "dev.morphia.morphia:validation:$morphiaVersion"
    api "org.bouncycastle:bcpkix-jdk15on:1.69" // use the same version as bcprov-jdk15on in DW

    // sda-commons-server-swagger
    api "io.openapitools.jackson.dataformat:jackson-dataformat-hal:1.0.9"
    api "io.swagger:swagger-annotations:$swaggerCoreVersion"
    api "io.swagger:swagger-jaxrs:$swaggerCoreVersion"

    // sda-commons-server-opentracing
    api "io.opentracing:opentracing-util:$openTracingUtilVersion"
    api "io.opentracing:opentracing-util:$openTracingUtilVersion:tests"
    api "io.opentracing:opentracing-mock:$openTracingUtilVersion"
    api "io.opentracing.contrib:opentracing-mongo-driver:0.1.5"
    api "io.opentracing.contrib:opentracing-aws-sdk-1:0.1.5"
    api "io.opentracing.contrib:opentracing-jaxrs2:1.0.0"
    api "io.opentracing.contrib:opentracing-concurrent:0.4.0"
    api "io.opentracing.contrib:opentracing-web-servlet-filter:0.4.1"

    // sda-commons-server-openapi
    api "io.swagger.core.v3:swagger-jaxrs2:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-jaxrs2-servlet-initializer-v2:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-annotations:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-core:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-models:$swaggerCoreV3Version"
    api "io.swagger.parser.v3:swagger-parser-v3:2.0.27"

    // sda-commons-server-prometheus
    api "io.prometheus:simpleclient:$prometheusVersion"
    api "io.prometheus:simpleclient_common:$prometheusVersion"
    api "io.prometheus:simpleclient_dropwizard:$prometheusVersion"
    api "io.prometheus:simpleclient_servlet:$prometheusVersion"

    // sda-commons-server-s3-testing
    api "io.findify:s3mock_2.12:0.2.6"

    // sda-commons-server-swagger
    api "com.github.java-json-tools:json-schema-validator:2.2.14"
    api "net.javacrumbs.json-unit:json-unit-assertj:$jsonUnitVersion"

    // sda-commons-server-testing
    api "org.hamcrest:hamcrest:2.2", {
      because "hamcrest is the successor of hamcrest-core."
    }
    api "org.hamcrest:hamcrest-core:2.2", {
      because '''\
          hamcrest-core is deprecated and overlaps with hamcrest, in hamcrest-core 2.2 is
          only one class named HamcrestCoreIsDeprecated. We force this version to avoid complex
          exclude and dependency substitution configuration as there is no conflict with
          hamcrest in the code base. Please do not upgrade hamcrest-core.
          '''
    }

    // sda-commons-server-weld
    api "org.jboss.weld.se:weld-se-core:$weldVersion"
    api "org.jboss.weld.servlet:weld-servlet-core:$weldVersion"
    api "javax.transaction:javax.transaction-api:1.3", {
      because "Dropwizard does not use the jakarta version yet so we replace it in weld with the javax version that comes as transitive dependency without explicit version."
    }

    api "org.junit-pioneer:junit-pioneer:1.4.2"

    // sda-commons-shared-*
    api 'commons-io:commons-io:2.11.0'

    // Kafka
    api "org.apache.kafka:kafka_2.12:$kafkaVersion"
    api "org.apache.kafka:kafka-clients:$kafkaVersion"
    api "org.apache.zookeeper:zookeeper:3.5.9"
    api "com.github.ftrossbach:club-topicana-core:0.1.0"
    api "com.salesforce.kafka.test:kafka-junit4:$kafkaJunitVersion"
    api "com.salesforce.kafka.test:kafka-junit5:$kafkaJunitVersion"
    api "org.apache.curator:curator-test:5.1.0", {
      because '''\
          Upgrade because kafka-junit still uses curator 2.12.0.
          The older version does not work when executing tests parallel because it starts the
          Zookeeper Admin Server on a fixed port. More recent versions do not start the admin server
          anymore.
        '''
    }
    api "org.xerial.snappy:snappy-java:1.1.7.7", {
      because "conflict between kafka-clients:2.6.1 & curator-test:5.1.0"
    }
    api "com.typesafe.scala-logging:scala-logging_2.12:3.9.2", {
      because "conflict between io.findify:s3mock_2.12 and org.apache.kafka:kafka_2.12 prefer version of kafka!"
    }
    api "org.scala-lang.modules:scala-java8-compat_2.12:0.9.1", {
      because "conflict between io.findify:s3mock_2.12 and org.apache.kafka:kafka_2.12 prefer version of kafka!"
    }
    api "org.scala-lang.modules:scala-collection-compat_2.12:2.3.0", {
      because "version from org.apache.kafka:kafka_2.12"
    }
    api "org.scala-lang:scala-library:$scalaVersion", {
      because "version from org.apache.kafka:kafka_2.12"
    }
    api "org.scala-lang:scala-reflect:$scalaVersion", {
      because "version from org.apache.kafka:kafka_2.12"
    }
    api "com.fasterxml.jackson.module:jackson-module-scala_2.12:2.10.5", {
      because "this is the requested version of org.apache.kafka:kafka_2.12:2.8.0"
    }
  }
}
