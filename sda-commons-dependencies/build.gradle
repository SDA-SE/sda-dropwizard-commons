plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  dropwizardVersion = '2.1.3'
  morphiaVersion = '1.6.1'
  prometheusVersion = '0.16.0'
  swaggerCoreVersion = '1.6.7'
  swaggerCoreV3Version = '2.2.3'
  weldVersion = '3.1.9.Final'
  openTracingUtilVersion = '0.33.0'
  jsonUnitVersion = '2.36.0'
  scalaVersion = '2.13.9' // align transitive dependency from various modules, keep up to date
  kafkaVersion = '3.2.3'
  kafkaJunitVersion = '3.2.4' // Matching version: https://github.com/salesforce/kafka-junit/tree/master/kafka-junit5
  dbRiderVersion = '1.34.0'
  kotlinVersion = '1.7.20'
  kotlinxCoroutinesVersion = '1.6.4'
}

dependencies {
  api enforcedPlatform("org.jetbrains.kotlin:kotlin-bom:$kotlinVersion") // override version from dropwizard-bom
  api enforcedPlatform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:$kotlinxCoroutinesVersion")
  api enforcedPlatform("io.dropwizard:dropwizard-bom:$dropwizardVersion")
  api enforcedPlatform("io.dropwizard:dropwizard-dependencies:$dropwizardVersion"), {
    exclude group: 'org.awaitility', module: 'awaitility'
  }
  api enforcedPlatform("com.amazonaws:aws-java-sdk-bom:1.12.316")
  api enforcedPlatform("io.github.resilience4j:resilience4j-bom:1.7.0")

  constraints {
    // overall conflicts
    api "org.apache.commons:commons-lang3:3.12.0"
    api "commons-codec:commons-codec:1.15", {
      because "conflict between org.apache.httpcomponents:httpclient:4.5.12 and org.dbunit:dbunit:2.7.0"
    }
    api "io.github.classgraph:classgraph:4.8.149", {
      because "conflict between io.swagger.core.v3:swagger-jaxrs2, io.swagger.core.v3:swagger-integration, dev.morphia.morphia:core and com.kjetland:mbknor-jackson-jsonschema_2.13 upgrade"
    }
    api "org.jboss.logging:jboss-logging:3.4.3.Final", {
      because "conflict between org.hibernate.validator:hibernate-validator and org.jboss.weld.se:weld-se-core"
    }
    api "org.yaml:snakeyaml:1.33", {
      because "conflict between com.github.ftrossbach:club-topicana-core and com.github.database-rider:rider-core and com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    }
    api "net.javacrumbs.json-unit:json-unit-core:$jsonUnitVersion", {
      because "conflict between com.github.tomakehurst:wiremock-jre8 and net.javacrumbs.json-unit:json-unit-assertj"
    }

    // overall testing dependencies
    api "org.awaitility:awaitility:4.2.0"

    // sda-commons-client-jersey-wiremock-testing
    api "com.github.tomakehurst:wiremock-jre8:2.34.0"
    api "org.ow2.asm:asm:9.4", {
      because "version conflict within Wiremock"
    }
    api "net.minidev:json-smart:2.4.8", {
      because 'conflict between json-path 2.7.0 (2.4.7) and wirmock-jre8 (2.4.7 via json-path and 2.4.8 directly)'
    }

    // sda-commons-shared-asyncapi
    api "com.kjetland:mbknor-jackson-jsonschema_2.13:1.0.39"

    // sda-commons-server-auth
    api "com.auth0:java-jwt:4.1.0"

    // sda-commons-server-circuitbreaker
    api "org.objenesis:objenesis:3.3"

    // sda-commons-server-hibernate
    api "org.postgresql:postgresql:42.5.0"
    api "org.flywaydb:flyway-core:9.4.0"

    // sda-commons-server-hibernate-testing
    api "com.github.database-rider:rider-core:$dbRiderVersion"
    api "com.github.database-rider:rider-junit5:$dbRiderVersion"

    // sda-commons-server-jaeger
    api "io.jaegertracing:jaeger-client:1.8.1"
    api "com.squareup.okhttp3:okhttp:4.10.0" // fixes CVE-2021-0341 (bump from 4.9.0 used in jaeger-thrift:1.8.0)

    // sda-commons-server-mongo-testing
    api "de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.5.1"

    // sda-commons-server-morphia
    api "dev.morphia.morphia:core:$morphiaVersion"
    api "dev.morphia.morphia:validation:$morphiaVersion"
    api "org.bouncycastle:bcpkix-jdk15on:1.70" // use the same version as bcprov-jdk15on in DW

    // sda-commons-server-openapi
    api "com.jayway.jsonpath:json-path:2.7.0"

    // sda-commons-server-swagger
    api "io.openapitools.jackson.dataformat:jackson-dataformat-hal:1.0.9"
    api "io.swagger:swagger-annotations:$swaggerCoreVersion"
    api "io.swagger:swagger-jaxrs:$swaggerCoreVersion"

    // sda-commons-server-opentracing
    api "io.opentracing:opentracing-util:$openTracingUtilVersion"
    api "io.opentracing:opentracing-util:$openTracingUtilVersion:tests"
    api "io.opentracing:opentracing-mock:$openTracingUtilVersion"
    api "io.opentracing.contrib:opentracing-mongo-driver:0.1.5"
    api "io.opentracing.contrib:opentracing-aws-sdk-1:0.1.5"
    api "io.opentracing.contrib:opentracing-jaxrs2:1.0.0"
    api "io.opentracing.contrib:opentracing-concurrent:0.4.0"
    api "io.opentracing.contrib:opentracing-web-servlet-filter:0.4.1"

    // sda-commons-server-openapi
    api "io.swagger.core.v3:swagger-jaxrs2:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-jaxrs2-servlet-initializer-v2:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-annotations:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-core:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-models:$swaggerCoreV3Version"
    api "io.swagger.parser.v3:swagger-parser-v3:2.1.3"

    // sda-commons-server-prometheus
    api "io.prometheus:simpleclient:$prometheusVersion"
    api "io.prometheus:simpleclient_common:$prometheusVersion"
    api "io.prometheus:simpleclient_dropwizard:$prometheusVersion"
    api "io.prometheus:simpleclient_servlet:$prometheusVersion"

    // sda-commons-server-s3-testing
    api "io.findify:s3mock_2.13:0.2.6"

    // sda-commons-server-swagger
    api "com.github.java-json-tools:json-schema-validator:2.2.14"
    api "net.javacrumbs.json-unit:json-unit-assertj:$jsonUnitVersion"

    // sda-commons-server-testing
    api "org.hamcrest:hamcrest:2.2", {
      because "hamcrest is the successor of hamcrest-core."
    }
    api "org.hamcrest:hamcrest-core:2.2", {
      because '''\
          hamcrest-core is deprecated and overlaps with hamcrest, in hamcrest-core 2.2 is
          only one class named HamcrestCoreIsDeprecated. We force this version to avoid complex
          exclude and dependency substitution configuration as there is no conflict with
          hamcrest in the code base. Please do not upgrade hamcrest-core.
          '''
    }

    // sda-commons-server-weld
    api "org.jboss.weld.se:weld-se-core:$weldVersion"
    api "org.jboss.weld.servlet:weld-servlet-core:$weldVersion"
    api "javax.transaction:javax.transaction-api:1.3", {
      because "Dropwizard does not use the jakarta version yet so we replace it in weld with the javax version that comes as transitive dependency without explicit version."
    }

    api "org.junit-pioneer:junit-pioneer:1.6.2"

    // sda-commons-shared-*
    api 'commons-io:commons-io:2.11.0'

    // Kafka
    api "org.apache.kafka:kafka_2.13:$kafkaVersion"
    api "org.apache.kafka:kafka-clients:$kafkaVersion"
    api "org.apache.zookeeper:zookeeper:3.5.9"
    api "com.github.ftrossbach:club-topicana-core:0.1.0"
    api "com.salesforce.kafka.test:kafka-junit4:$kafkaJunitVersion"
    api "com.salesforce.kafka.test:kafka-junit5:$kafkaJunitVersion"
    api "org.apache.curator:curator-test:5.3.0", {
      because '''\
          Upgrade because kafka-junit still uses curator 2.12.0.
          The older version does not work when executing tests parallel because it starts the
          Zookeeper Admin Server on a fixed port. More recent versions do not start the admin server
          anymore.
        '''
    }
    api "org.xerial.snappy:snappy-java:1.1.8.4", {
      because "conflict between org.apache.kafka:kafka-clients and org.apache.curator:curator-test upgrade"
    }
    api "org.scala-lang.modules:scala-java8-compat_2.13:1.0.2", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13 upgrade"
    }
    api "org.scala-lang.modules:scala-collection-compat_2.13:2.8.1", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13 upgrade"
    }
    api "org.scala-lang:scala-library:$scalaVersion", {
      because "conflict between org.apache.kafka:kafka_2.13, io.findify:s3mock_2.13 and com.kjetland:mbknor-jackson-jsonschema_2.13:+ upgrade"
    }
    api "org.scala-lang:scala-reflect:$scalaVersion", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13"
    }
    api "com.typesafe.scala-logging:scala-logging_2.13:3.9.5", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13"
    }
  }
}
