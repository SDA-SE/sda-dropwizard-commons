plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  dropwizardVersion = '2.1.6'
  prometheusVersion = '0.16.0'
  swaggerCoreVersion = '1.6.9'
  swaggerCoreV3Version = '2.2.9'
  weldVersion = '3.1.9.Final'
  jsonUnitVersion = '2.37.0'
  scalaVersion = '2.13.10' // align transitive dependency from various modules, keep up to date
  kafkaVersion = '3.4.0'
  kafkaJunitVersion = '3.2.5' // Matching version: https://github.com/salesforce/kafka-junit/tree/master/kafka-junit5
  dbRiderVersion = '1.36.0'
  kotlinVersion = '1.8.20'
  kotlinxCoroutinesVersion = '1.6.4'
  resilience4jVersion = '1.7.1'
  otelAgentAlphaVersion = '1.16.0-alpha'
  openTelemetryAlphaVersion = '1.16.0-alpha'
  openTelemetryVersion= '1.16.0' // Matching version: https://github.com/open-telemetry/opentelemetry-java
  openTelemetryAlphaVersion = '1.16.0-alpha'
  otelInstrumentationAlphaVersion = '1.16.0-alpha'
}

dependencies {
  api enforcedPlatform("org.jetbrains.kotlin:kotlin-bom:$kotlinVersion") // override version from dropwizard-bom
  api enforcedPlatform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:$kotlinxCoroutinesVersion")
  api enforcedPlatform("io.dropwizard:dropwizard-bom:$dropwizardVersion")
  api enforcedPlatform("io.dropwizard:dropwizard-dependencies:$dropwizardVersion"), {
    exclude group: 'org.awaitility', module: 'awaitility'
    exclude group: 'org.assertj', module: 'assertj-core'
    exclude group: 'jakarta.xml.bind', module: 'jakarta.xml.bind-api'
    exclude group: 'org.junit', module: 'junit'
  }
  api enforcedPlatform("com.amazonaws:aws-java-sdk-bom:1.12.456")
  api enforcedPlatform("io.opentelemetry:opentelemetry-bom:$openTelemetryVersion")
  api enforcedPlatform("io.opentelemetry:opentelemetry-bom-alpha:$openTelemetryAlphaVersion")

  constraints {
    // overall conflicts
    api "org.apache.commons:commons-lang3:3.12.0"
    api "commons-codec:commons-codec:1.15", {
      because "conflict between org.apache.httpcomponents:httpclient:4.5.12 and org.dbunit:dbunit:2.7.0"
    }
    api "io.github.classgraph:classgraph:4.8.157", {
      because "conflict between io.swagger.core.v3:swagger-jaxrs2, io.swagger.core.v3:swagger-integration, dev.morphia.morphia:core and com.kjetland:mbknor-jackson-jsonschema_2.13 upgrade"
    }
    api "org.jboss.logging:jboss-logging:3.5.0.Final", {
      because "conflict between org.hibernate.validator:hibernate-validator and org.jboss.weld.se:weld-se-core"
    }
    api "org.yaml:snakeyaml:1.33", {
      because "conflict between com.github.ftrossbach:club-topicana-core and com.github.database-rider:rider-core and com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    }
    api "net.javacrumbs.json-unit:json-unit-core:$jsonUnitVersion", {
      because "conflict between com.github.tomakehurst:wiremock-jre8 and net.javacrumbs.json-unit:json-unit-assertj"
    }

    // overall testing dependencies
    api "org.awaitility:awaitility:4.2.0"
    api 'org.assertj:assertj-core:3.24.2'

    // sda-commons-client-jersey-wiremock-testing
    api "com.github.tomakehurst:wiremock-jre8:2.35.0"
    api "org.ow2.asm:asm:9.5", {
      because "version conflict within Wiremock"
    }
    api "net.minidev:json-smart:2.4.10", {
      because 'conflict between json-path 2.7.0 (2.4.7) and wirmock-jre8 (2.4.7 via json-path and 2.4.8 directly)'
    }

    // sda-commons-shared-asyncapi
    api "com.kjetland:mbknor-jackson-jsonschema_2.13:1.0.39"

    // sda-commons-server-auth
    api "com.auth0:java-jwt:4.4.0"
    api "org.bouncycastle:bcpkix-jdk15on:1.70" // use the same version as bcprov-jdk15on in DW

    // sda-commons-server-circuitbreaker
    api "io.github.resilience4j:resilience4j-circuitbreaker:$resilience4jVersion"
    api "io.github.resilience4j:resilience4j-prometheus:$resilience4jVersion"
    api "org.objenesis:objenesis:3.3"

    // sda-commons-server-hibernate
    api "org.postgresql:postgresql:42.6.0"
    api "org.flywaydb:flyway-core:9.16.3"

    // sda-commons-server-hibernate-testing
    api "com.github.database-rider:rider-core:$dbRiderVersion"
    api "com.github.database-rider:rider-junit5:$dbRiderVersion"

    // sda-commons-server-mongo-testing
    api "de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.5.4"
    api "io.opentelemetry.instrumentation:opentelemetry-mongo-3.1:$otelAgentAlphaVersion"

    // sda-commons-server-openapi
    api "com.jayway.jsonpath:json-path:2.8.0"

    // sda-commons-server-openapi
    api "io.openapitools.jackson.dataformat:jackson-dataformat-hal:1.0.9" // TODO Needed for OpenAPI?
    api "io.swagger.core.v3:swagger-jaxrs2:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-jaxrs2-servlet-initializer-v2:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-annotations:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-core:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-models:$swaggerCoreV3Version"
    api "io.swagger.parser.v3:swagger-parser-v3:2.1.13"

    // sda-commons-server-prometheus
    api "io.prometheus:simpleclient:$prometheusVersion"
    api "io.prometheus:simpleclient_common:$prometheusVersion"
    api "io.prometheus:simpleclient_dropwizard:$prometheusVersion"
    api "io.prometheus:simpleclient_servlet:$prometheusVersion"

    // sda-commons-server-s3-testing
    api "io.findify:s3mock_2.13:0.2.6"
    api "io.opentelemetry.instrumentation:opentelemetry-aws-sdk-1.11:$otelInstrumentationAlphaVersion"

    // sda-commons-server-swagger
    api "com.github.java-json-tools:json-schema-validator:2.2.14"
    api "net.javacrumbs.json-unit:json-unit-assertj:$jsonUnitVersion"

    // sda-commons-server-testing
    api "org.hamcrest:hamcrest:2.2", {
      because "hamcrest is the successor of hamcrest-core."
    }
    api "org.hamcrest:hamcrest-core:2.2", {
      because '''\
          hamcrest-core is deprecated and overlaps with hamcrest, in hamcrest-core 2.2 is
          only one class named HamcrestCoreIsDeprecated. We force this version to avoid complex
          exclude and dependency substitution configuration as there is no conflict with
          hamcrest in the code base. Please do not upgrade hamcrest-core.
          '''
    }

    // sda-commons-server-weld
    api "org.jboss.weld.se:weld-se-core:$weldVersion"
    api "org.jboss.weld.servlet:weld-servlet-core:$weldVersion"
    api "javax.transaction:javax.transaction-api:1.3", {
      because "Dropwizard does not use the jakarta version yet so we replace it in weld with the javax version that comes as transitive dependency without explicit version."
    }

    api "org.junit-pioneer:junit-pioneer:2.0.0"

    // sda-commons-shared-*
    api 'commons-io:commons-io:2.11.0'

    // sda-commons-server-opentelemetry
    api "io.opentelemetry:opentelemetry-sdk-extension-autoconfigure:$openTelemetryAlphaVersion"
    api "io.opentelemetry.instrumentation:opentelemetry-apache-httpclient-4.3:$otelInstrumentationAlphaVersion"
    api "io.opentelemetry:opentelemetry-exporter-otlp:$openTelemetryVersion"
    api "io.opentelemetry:opentelemetry-exporter-jaeger:$openTelemetryVersion"
    api "io.opentelemetry:opentelemetry-extension-trace-propagators:$openTelemetryVersion"
    api "io.opentelemetry:opentelemetry-sdk-testing:$openTelemetryVersion"
    api "io.opentelemetry:opentelemetry-exporter-logging:$openTelemetryVersion"
    api "com.squareup.okhttp3:okhttp:4.11.0" // bump from 4.9.3 used in opentelemetry-exporter-otlp-common

    // Kafka
    api "org.apache.kafka:kafka_2.13:$kafkaVersion"
    api "org.apache.kafka:kafka-clients:$kafkaVersion"
    api "org.apache.zookeeper:zookeeper:3.5.9"
    api "com.github.ftrossbach:club-topicana-core:0.1.0"
    api "com.salesforce.kafka.test:kafka-junit5:$kafkaJunitVersion"
    api "org.apache.curator:curator-test:5.4.0", {
      because '''\
          Upgrade because kafka-junit still uses curator 2.12.0.
          The older version does not work when executing tests parallel because it starts the
          Zookeeper Admin Server on a fixed port. More recent versions do not start the admin server
          anymore.
        '''
    }
    api "org.xerial.snappy:snappy-java:1.1.8.4", {
      because "conflict between org.apache.kafka:kafka-clients and org.apache.curator:curator-test upgrade"
    }
    api "org.scala-lang.modules:scala-java8-compat_2.13:1.0.2", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13 upgrade"
    }
    api "org.scala-lang.modules:scala-collection-compat_2.13:2.9.0", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13 upgrade"
    }
    api "org.scala-lang:scala-library:$scalaVersion", {
      because "conflict between org.apache.kafka:kafka_2.13, io.findify:s3mock_2.13 and com.kjetland:mbknor-jackson-jsonschema_2.13:+ upgrade"
    }
    api "org.scala-lang:scala-reflect:$scalaVersion", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13"
    }
    api "com.typesafe.scala-logging:scala-logging_2.13:3.9.5", {
      because "conflict between io.findify:s3mock_2.13 and org.apache.kafka:kafka_2.13"
    }

    api "jakarta.xml.bind:jakarta.xml.bind-api:4.0.0"
    api 'javax.xml.bind:jaxb-api:2.3.1'
  }
}
