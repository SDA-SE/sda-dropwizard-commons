plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  antlr4Version = '4.13.2'
  apacheHttpClient5Version = '5.5.1'
  // Check on upgrade, if org.apache.commons:commons-lang3 enforcement below (L48 + L73) is still needed!
  dropwizardVersion = '4.0.15'
  prometheusVersion = '0.16.0'
  micrometerVersion = '1.15.4'
  swaggerCoreV3Version = '2.2.37'
  weldVersion = '5.1.2.Final'
  jacksonVersion = '2.19.2'
  jsonUnitVersion = '4.1.1'
  scalaVersion = '2.13.16' // align transitive dependency from various modules, keep up to date
  kafkaVersion = '3.9.1'
  kafkaJunitVersion = '3.2.5' // Matching version: https://github.com/salesforce/kafka-junit/tree/master/kafka-junit5
  dbRiderVersion = '1.44.0'
  kotlinVersion = '2.2.20'
  kotlinxCoroutinesVersion = '1.10.2'
  resilience4jVersion = '2.2.0'
  openTelemetryVersion = '1.41.0'
  openTelemetryAlpha2Version = '2.20.1-alpha'
  // should be aligned with transitive dependency of Spring Data MongoDB
  mongoDbDriverVersion = '5.6.0'
  tuprologVersion = '0.20.9' // don't upgrade separately! use version from database-rider
  bouncyCastleVersion = '1.82'
  victoolsVersion = '4.38.0'
  flywayVersion = '11.13.2'
}

dependencies {
  // overwrite version used by Dropwizard to comply with Snakeyaml 2.0
  api enforcedPlatform("com.fasterxml.jackson:jackson-bom:${jacksonVersion}")
  // override version from dropwizard-bom
  api enforcedPlatform("org.jetbrains.kotlin:kotlin-bom:$kotlinVersion")
  api enforcedPlatform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:$kotlinxCoroutinesVersion")
  api enforcedPlatform("org.eclipse.jetty:jetty-bom:11.0.26"), {
    because("dropwizard and dropwizard-metrics use different versions")
  }
  api enforcedPlatform("io.dropwizard:dropwizard-bom:$dropwizardVersion")
  api enforcedPlatform("io.dropwizard:dropwizard-dependencies:$dropwizardVersion"), {
    exclude group: 'jakarta.xml.bind', module: 'jakarta.xml.bind-api'
    exclude group: 'com.fasterxml.jackson'
    exclude group: 'com.fasterxml.jackson.core'
    exclude group: 'com.fasterxml.jackson.dataformat'
    exclude group: 'com.fasterxml.jackson.datatype'
    exclude group: 'com.fasterxml.jackson.jaxrs'
    exclude group: 'com.fasterxml.jackson.module'
    exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
  }
  api enforcedPlatform("software.amazon.awssdk:bom:2.35.0")
  api enforcedPlatform("jakarta.platform:jakarta.jakartaee-bom:11.0.0")
  api enforcedPlatform("io.opentelemetry:opentelemetry-bom:$openTelemetryVersion")
  api enforcedPlatform("io.opentelemetry:opentelemetry-bom-alpha:${openTelemetryVersion}-alpha")
  api enforcedPlatform("io.netty:netty-bom:4.2.6.Final"), {
    because "various CVEs in earlier versions"
  }
  api enforcedPlatform("io.prometheus:simpleclient_bom:${prometheusVersion}")

  // asyncapi generation
  api enforcedPlatform("com.github.victools:jsonschema-generator-bom:${victoolsVersion}")

  constraints {
    // overall conflicts
    api "org.apache.httpcomponents.client5:httpclient5:${apacheHttpClient5Version}"
    api "org.apache.httpcomponents.client5:httpclient5-fluent:${apacheHttpClient5Version}"
    api "io.github.classgraph:classgraph:4.8.181"

    api "org.jboss.logging:jboss-logging:3.6.1.Final", {
      because "conflict between org.hibernate.validator:hibernate-validator and org.jboss.weld.se:weld-se-core"
    }
    api "org.yaml:snakeyaml:2.3", {
      because "conflict between com.github.database-rider:rider-core and com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
      because "vulnerability CVE-2022-1471 in 1.33 and below"
    }

    api 'commons-beanutils:commons-beanutils:1.11.0', {
      because "vulnerability CVE-2025-48734 in versions < 1.11.0"
    }

    api 'jakarta.annotation:jakarta.annotation-api:2.1.1'

    // sda-commons-shared-wiremock-testing
    api "org.wiremock:wiremock:3.13.1"
    api "net.minidev:json-smart:2.6.0", {
      because 'conflict between json-path 2.7.0 (2.4.7) and wirmock-jre8 (2.4.7 via json-path and 2.4.8 directly)'
    }

    // sda-commons-shared-asyncapi
    api "io.swagger.core.v3:swagger-models-jakarta:${swaggerCoreV3Version}"

    // sda-commons-server-auth
    api "com.auth0:java-jwt:4.5.0"
    api "org.bouncycastle:bcpkix-jdk18on:$bouncyCastleVersion"
    api "org.bouncycastle:bcprov-jdk18on:$bouncyCastleVersion"

    // sda-commons-server-circuitbreaker
    api "io.github.resilience4j:resilience4j-circuitbreaker:$resilience4jVersion"
    api "io.github.resilience4j:resilience4j-micrometer:${resilience4jVersion}"
    api "org.objenesis:objenesis:3.4"

    // sda-commons-server-hibernate
    api "org.postgresql:postgresql:42.7.8"
    api "org.flywaydb:flyway-core:${flywayVersion}"
    api "org.flywaydb:flyway-database-postgresql:${flywayVersion}"
    // conflict between Flyway and tuprolog via DB Rider
    api "org.antlr:antlr4-runtime:${antlr4Version}"

    // sda-commons-server-hibernate-testing
    api "com.github.database-rider:rider-core:$dbRiderVersion"
    api "com.github.database-rider:rider-junit5:$dbRiderVersion"
    api "it.unibo.tuprolog:solve-classic-jvm:$tuprologVersion"
    api "it.unibo.tuprolog:parser-theory-jvm:$tuprologVersion"

    // sda-commons-server-mongo-testing, sda-commons-server-spring-data-mongo
    // Keep that version for now, since 4.2.0 introduces a bug.
    // Newer Versions contain breaking change. Update, when v5 gets released
    api 'org.springframework.data:spring-data-mongodb:4.1.12'
    api "de.flapdoodle.embed:de.flapdoodle.embed.mongo:4.11.1"
    api "io.opentelemetry.instrumentation:opentelemetry-mongo-3.1:${openTelemetryAlpha2Version}"
    // check if commons-compress management is still needed after flapdoodle upgrade
    // flapdoodle 4.11.1 pulls commons-compress 1.25.0 with CVEs 2024-25710 and 2024-26308
    api "org.apache.commons:commons-compress:1.28.0"
    api "org.mongodb:mongodb-driver-core:${mongoDbDriverVersion}"
    api "org.mongodb:mongodb-driver-sync:${mongoDbDriverVersion}"
    api "org.mongodb:mongodb-driver-legacy:${mongoDbDriverVersion}"

    // sda-commons-server-openapi
    api "com.jayway.jsonpath:json-path:2.9.0"

    // sda-commons-server-openapi
    api "io.openapitools.jackson.dataformat:jackson-dataformat-hal:1.0.9" // TODO Needed for OpenAPI?
    api "io.swagger.core.v3:swagger-jaxrs2-jakarta:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-jaxrs2-servlet-initializer-v2-jakarta:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-annotations-jakarta:$swaggerCoreV3Version"
    api "io.swagger.parser.v3:swagger-parser-v3:2.1.34"

    api "io.micrometer:micrometer-core:$micrometerVersion"
    api "io.micrometer:micrometer-registry-prometheus-simpleclient:$micrometerVersion", {
      because '''\
          Dropwizard 4 is not compatible to Java client 1.x version.
          This module is deprecated, so when we upgrade to Dropwizard 5 we should
          migrate to io.micrometer:micrometer-registry-prometheus:1.13.0.
          See https://github.com/micrometer-metrics/micrometer/wiki/1.13-Migration-Guide#prometheus-java-client-0x-to-1x-upgrade
          '''
    }

    // sda-commons-server-s3
    api "org.reactivestreams:reactive-streams:1.0.4"
    api "io.opentelemetry.instrumentation:opentelemetry-aws-sdk-2.2:${openTelemetryAlpha2Version}"

    // sda-commons-server-s3-testing
    api "io.findify:s3mock_2.13:0.2.6"

    // sda-commons-server-swagger
    api "com.github.java-json-tools:json-schema-validator:2.2.14"
    api "net.javacrumbs.json-unit:json-unit-assertj:$jsonUnitVersion"

    // sda-commons-server-testing
    api "org.hamcrest:hamcrest:3.0", {
      because "hamcrest is the successor of hamcrest-core."
    }
    api "org.hamcrest:hamcrest-core:3.0", {
      because '''\
          hamcrest-core is deprecated and overlaps with hamcrest, in hamcrest-core 2.2 is
          only one class named HamcrestCoreIsDeprecated. We force this version to avoid complex
          exclude and dependency substitution configuration as there is no conflict with
          hamcrest in the code base. Please do not upgrade hamcrest-core without verifying the
          deprecation.
          '''
    }

    // sda-commons-server-weld
    api "org.jboss.weld.se:weld-se-core:$weldVersion"
    api "org.jboss.weld.servlet:weld-servlet-core:$weldVersion"

    api "org.junit-pioneer:junit-pioneer:2.3.0"

    // sda-commons-server-weld-testing
    api 'org.jboss.weld:weld-junit5:4.0.5.Final'

    // sda-commons-shared-*
    api 'commons-io:commons-io:2.20.0'

    // sda-commons-server-opentelemetry
    api "io.opentelemetry.javaagent.instrumentation:opentelemetry-javaagent-apache-httpclient-5.0:${openTelemetryAlpha2Version}"
    api "io.opentelemetry.instrumentation:opentelemetry-apache-httpclient-5.2:${openTelemetryAlpha2Version}"
    api "io.opentelemetry.semconv:opentelemetry-semconv:1.37.0"
    api "io.opentelemetry.instrumentation:opentelemetry-instrumentation-api:2.20.1"
    api "io.opentelemetry.instrumentation:opentelemetry-instrumentation-api-semconv:1.33.6-alpha"

    // Kafka
    api "org.apache.kafka:kafka_2.13:$kafkaVersion"
    api "org.apache.kafka:kafka-clients:$kafkaVersion"
    api "org.apache.zookeeper:zookeeper:3.9.4"
    api "com.salesforce.kafka.test:kafka-junit5:$kafkaJunitVersion"
    api "org.apache.curator:curator-test:5.9.0", {
      because '''\
          Upgrade because kafka-junit still uses curator 2.12.0.
          The older version does not work when executing tests parallel because it starts the
          Zookeeper Admin Server on a fixed port. More recent versions do not start the admin server
          anymore.
        '''
    }
    api "org.xerial.snappy:snappy-java:1.1.10.8", {
      because "conflict between org.apache.kafka:kafka-clients and org.apache.curator:curator-test upgrade"
    }

    // Scala: Version conflicts between Jackson and Kafka
    api "org.scala-lang.modules:scala-java8-compat_2.13:1.0.2"
    api "org.scala-lang.modules:scala-collection-compat_2.13:2.13.0"
    api "org.scala-lang:scala-library:$scalaVersion"
    api "org.scala-lang:scala-reflect:$scalaVersion"
    api "com.typesafe.scala-logging:scala-logging_2.13:3.9.6"

    api "jakarta.xml.bind:jakarta.xml.bind-api:4.0.4"

    //spring-data-mongodb can't be updated without breaking changes.
    //Instead we can add the spring-core dependency to fix spring related CVEs.
    api 'org.springframework:spring-core:6.2.11'
  }
}
