plugins {
  id 'java-platform'
}

javaPlatform {
  allowDependencies() // enable importing other BOMs
}

ext {
  dropwizardVersion = '2.0.15'
  morphiaVersion = '1.6.0'
  prometheusVersion = '0.9.0'
  swaggerCoreVersion = '1.6.2'
  swaggerCoreV3Version = '2.1.5'
  weldVersion = '3.1.5.Final'
  openTracingUtilVersion = '0.33.0'
  jsonUnitVersion = '2.21.0'
  scalaVersion = '2.12.8' // Always prefer the version from kafka_2.12. Make sure that the major version is always aligned with kafka, s3mock, and mbknor-jackson-jsonschema.
  kafkaVersion = '2.2.1' // Fix version as decided in https://sda-se.atlassian.net/l/c/ce8EFM2e. Be careful when upgrading this dependency.
}

dependencies {
  api enforcedPlatform("org.jetbrains.kotlin:kotlin-bom:1.3.50") // override version from dropwizard-bom
  api enforcedPlatform("io.dropwizard:dropwizard-bom:$dropwizardVersion")
  api enforcedPlatform("io.dropwizard:dropwizard-dependencies:$dropwizardVersion")
  api enforcedPlatform("com.amazonaws:aws-java-sdk-bom:1.11.858")
  api enforcedPlatform("io.github.resilience4j:resilience4j-bom:1.6.1")

  constraints {
    // overall conflicts
    api "commons-codec:commons-codec:1.15", {
      because "conflict between org.apache.httpcomponents:httpclient:4.5.12 and org.dbunit:dbunit:2.7.0"
    }
    api "io.github.classgraph:classgraph:4.8.90", {
      because "conflict between com.kjetland:mbknor-jackson-jsonschema_2.12 and dev.morphia.morphia:core"
    }
    api "net.sf.jopt-simple:jopt-simple:5.0.4", {
      because "conflict between org.apache.kafka:kafka_2.12 and com.github.tomakehurst:wiremock-jre8"
    }
    api "org.jboss.logging:jboss-logging:3.4.1.Final", {
      because "conflict between org.hibernate.validator:hibernate-validator and org.jboss.weld.se:weld-se-core"
    }
    api "org.yaml:snakeyaml:1.26", {
      because "conflict between com.github.ftrossbach:club-topicana-core and com.github.database-rider:rider-core and com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    }
    api "net.javacrumbs.json-unit:json-unit-core:$jsonUnitVersion", {
      because "conflict between com.github.tomakehurst:wiremock-jre8 and net.javacrumbs.json-unit:json-unit-assertj"
    }

    // overall testing dependencies
    api "org.awaitility:awaitility:4.0.3"

    // sda-commons-client-jersey-wiremock-testing
    api "com.github.tomakehurst:wiremock-jre8:2.27.2"

    // sda-commons-server-asyncapi
    api "com.kjetland:mbknor-jackson-jsonschema_2.12:1.0.39"

    // sda-commons-server-auth
    api "com.auth0:java-jwt:3.11.0"

    // sda-commons-server-circuitbreaker
    api "org.objenesis:objenesis:3.1"

    // sda-commons-server-hibernate
    api "org.postgresql:postgresql:42.2.18"
    api "org.flywaydb:flyway-core:7.2.0"

    // sda-commons-server-hibernate-testing
    api "com.github.database-rider:rider-core:1.2.9", {
      because "latest version that supports Parameterized tests: https://github.com/database-rider/database-rider/issues/104"
    }
    api "org.dbunit:dbunit:2.7.0", {
      because "rider in 1.2.9 only provides 2.5.3 from 2016, see http://dbunit.sourceforge.net/changes-report.html"
    }

    // sda-commons-server-jaeger
    api "io.jaegertracing:jaeger-client:1.4.0"

    // sda-commons-server-mongo-testing
    api "de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.0.0"

    // sda-commons-server-morphia
    api "dev.morphia.morphia:core:$morphiaVersion"
    api "dev.morphia.morphia:validation:$morphiaVersion"
    api "org.bouncycastle:bcpkix-jdk15on:1.67" // use the same version as bcprov-jdk15on in DW

    // sda-commons-server-swagger
    api "io.openapitools.hal:swagger-hal:1.0.4"
    api "io.openapitools.jackson.dataformat:jackson-dataformat-hal:1.0.7"
    api "io.swagger:swagger-annotations:$swaggerCoreVersion"
    api "io.swagger:swagger-jaxrs:$swaggerCoreVersion"

    // sda-commons-server-opentracing
    api "io.opentracing:opentracing-util:$openTracingUtilVersion"
    api "io.opentracing:opentracing-util:$openTracingUtilVersion:tests"
    api "io.opentracing:opentracing-mock:$openTracingUtilVersion"
    api "io.opentracing.contrib:opentracing-mongo-driver:0.1.5"
    api "io.opentracing.contrib:opentracing-aws-sdk-1:0.1.5"
    api "io.opentracing.contrib:opentracing-jaxrs2:1.0.0"
    api "io.opentracing.contrib:opentracing-concurrent:0.4.0"
    api "io.opentracing.contrib:opentracing-web-servlet-filter:0.4.1"

    // sda-commons-server-openapi
    api "io.swagger.core.v3:swagger-jaxrs2:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-jaxrs2-servlet-initializer-v2:$swaggerCoreV3Version"
    api "io.swagger.core.v3:swagger-annotations:$swaggerCoreV3Version"
    api "io.swagger.parser.v3:swagger-parser-v3:2.0.23"

    // sda-commons-server-prometheus
    api "io.prometheus:simpleclient:$prometheusVersion"
    api "io.prometheus:simpleclient_common:$prometheusVersion"
    api "io.prometheus:simpleclient_dropwizard:$prometheusVersion"
    api "io.prometheus:simpleclient_servlet:$prometheusVersion"

    // sda-commons-server-s3-testing
    api "io.findify:s3mock_2.12:0.2.6"

    // sda-commons-server-swagger
    api "com.github.java-json-tools:json-schema-validator:2.2.10"
    api "net.javacrumbs.json-unit:json-unit-assertj:$jsonUnitVersion"

    // sda-commons-server-testing
    api "org.hamcrest:hamcrest:2.2", {
      because "hamcrest is the successor of hamcrest-core."
    }
    api "org.hamcrest:hamcrest-core:2.2", {
      because '''\
          hamcrest-core is deprecated and overlaps with hamcrest, in hamcrest-core 2.2 is
          only one class named HamcrestCoreIsDeprecated. We force this version to avoid complex
          exclude and dependency substitution configuration as there is no conflict with
          hamcrest in the code base. Please do not upgrade hamcrest-core.
          '''
    }

    // sda-commons-server-weld
    api "org.jboss.weld.se:weld-se-core:$weldVersion"
    api "org.jboss.weld.servlet:weld-servlet-core:$weldVersion"
    api "javax.transaction:javax.transaction-api:1.3", {
      because "Dropwizard does not use the jakarta version yet so we replace it in weld with the javax version that comes as transitive dependency without explicit version."
    }

    // sda-commons-shared-*
    api 'commons-io:commons-io:2.8.0'

    // Kafka
    api "org.apache.kafka:kafka_2.12:$kafkaVersion"
    api "org.apache.kafka:kafka-clients:$kafkaVersion"
    api "org.apache.zookeeper:zookeeper:3.4.10"
    api "com.101tec:zkclient:0.11"
    api "org.apache.curator:curator-test:2.12.0"
    api "com.github.ftrossbach:club-topicana-core:0.1.0"
    api "com.salesforce.kafka.test:kafka-junit4:3.2.2"
    api "com.typesafe.scala-logging:scala-logging_2.12:3.9.2", {
      because "conflict between io.findify:s3mock_2.12 and org.apache.kafka:kafka_2.12 prefer version of kafka!"
    }
    api "org.scala-lang:scala-library:$scalaVersion", {
      because "version from org.apache.kafka:kafka_2.12"
    }
    api "org.scala-lang:scala-reflect:$scalaVersion", {
      because "version from org.apache.kafka:kafka_2.12"
    }
  }
}
