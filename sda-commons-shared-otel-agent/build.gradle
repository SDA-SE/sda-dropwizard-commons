plugins{
  id "com.github.johnrengelman.shadow" version "7.1.2"
}
ext {
  otelAgentVersion = '1.13.1'
  otelAgentAlphaVersion = '1.13.1-alpha'

  autoservice= dependencies.create(group: 'com.google.auto.service', name: 'auto-service', version: '1.0')
}

configurations {
  agents
  // Interfaces that are used to compile the SPI are also needed for testing
  testImplementation.extendsFrom compileOnly
}

dependencies {
  testImplementation project(':sda-commons-server-testing')
  testImplementation 'org.assertj:assertj-core'

  // JAVA Agent for the auto instrumentation
  agents "io.opentelemetry.javaagent:opentelemetry-javaagent:$otelAgentVersion"

  // Interfaces and SPIs for extending the agent.
  compileOnly("io.opentelemetry.javaagent:opentelemetry-javaagent-extension-api:$otelAgentAlphaVersion"){
    exclude group: 'io.opentelemetry' , module: 'opentelemetry-sdk'
  }

  //Provides @AutoService annotation to register SPIs
  compileOnly autoservice
  annotationProcessor autoservice


  //Produces a copy of upstream javaagent with this extension jar included inside it
  //The location of extension directory inside agent jar is hard-coded in the agent source code
  task extendedAgent(type: Jar) {
    dependsOn(configurations.agents)
    from zipTree(configurations.agents.singleFile)
    from(tasks.shadowJar.archiveFile) {
      into "extensions"
    }

    //Preserve MANIFEST.MF file from the upstream javaagent
    doFirst {
      manifest.from(
          zipTree(configurations.agents.singleFile).matching {
            include 'META-INF/MANIFEST.MF'
          }.singleFile
          )
    }
  }

  jar {
    actions.clear()
    dependsOn(['extendedAgent'])
  }
}