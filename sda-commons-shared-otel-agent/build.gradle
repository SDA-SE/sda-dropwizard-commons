plugins{
  id "com.github.johnrengelman.shadow" version "7.1.2"
}

configurations {
  agents
}

dependencies {
  agents platform(project(':sda-commons-dependencies'))
  annotationProcessor platform(project(':sda-commons-dependencies'))

  // Interfaces and SPIs for extending the agent.
  compileOnly("io.opentelemetry.javaagent:opentelemetry-javaagent-extension-api")

  //Provides @AutoService annotation to register SPIs
  compileOnly "com.google.auto.service:auto-service"
  annotationProcessor "com.google.auto.service:auto-service"

  // JAVA Agent for the auto instrumentation
  agents "io.opentelemetry.javaagent:opentelemetry-javaagent"

  testImplementation("io.opentelemetry.javaagent:opentelemetry-javaagent-extension-api")
  testImplementation project(':sda-commons-server-testing')
  testImplementation 'org.assertj:assertj-core'



  //Produces a copy of upstream javaagent with this extension jar included inside it
  //The location of extension directory inside agent jar is hard-coded in the agent source code
  task extendedAgent(type: Jar) {
    dependsOn(configurations.agents)
    from zipTree(configurations.agents.singleFile)
    from(tasks.shadowJar.archiveFile) {
      into "extensions"
    }

    //Preserve MANIFEST.MF file from the upstream javaagent
    doFirst {
      manifest.from(
          zipTree(configurations.agents.singleFile).matching {
            include 'META-INF/MANIFEST.MF'
          }.singleFile
          )
    }
  }

  jar {
    actions.clear()
    dependsOn(['extendedAgent'])
  }
}